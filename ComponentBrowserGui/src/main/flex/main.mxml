<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml"
				xmlns:common="clarin.cmdi.componentregistry.common.components.*"
				layout="absolute"
				preinitialize="init();"
				applicationComplete="appComplete();"
				backgroundColor="#DDE3EF">
	<mx:Style source="/assets/css/main.css"/>
	<mx:Script>
		<![CDATA[
			import clarin.cmdi.componentregistry.common.AboutPopup;
			import clarin.cmdi.componentregistry.common.Credentials;
			import clarin.cmdi.componentregistry.services.Config;
			import clarin.cmdi.componentregistry.services.PingSessionService;
			
			import com.flexspy.FlexSpy;
			
			import flash.utils.getTimer;
			
			import mx.containers.Box;
			import mx.controls.Alert;
			import mx.controls.Button;
			import mx.core.UIComponent;
			import mx.core.mx_internal;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			
			import org.hasseg.externalMouseWheel.ExternalMouseWheelSupport;

			private var sessionPinger:PingSessionService;
			[Bindable]
			private var aboutMenu:ContextMenu;
			
			
			private function init():void{
				this.contextMenu = createMenu();
				initializeInstances();
			}
			
			private function initializeInstances():void {
				Credentials.create(Application.application.parameters);
				Config.create(Application.application.parameters);				
			}
			
			private function createMenu():ContextMenu{
				var menu:ContextMenu = new ContextMenu();
				var item:ContextMenuItem = new ContextMenuItem("About Clarin Component Registry");
				item.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT, showAbout);
				menu.customItems = [item];
				menu.hideBuiltInItems();
				return menu;
			}
			
			private function showAbout(event:ContextMenuEvent):void{
				var aboutBox:AboutPopup = new AboutPopup();
				aboutBox.x = appPanel.width/2 - aboutBox.width/2;
				aboutBox.y = appPanel.height/2 - aboutBox.height/2;
				PopUpManager.addPopUp(aboutBox, event.mouseTarget);
			}

			private function appComplete():void {
				ExternalMouseWheelSupport.getInstance(stage);
				sessionPinger = PingSessionService.INSTANCE
				sessionPinger.startPinging();
				if (Credentials.instance.isLoggedIn() && (Config.instance.space == Config.SPACE_USER)) {
					Config.instance.userSpace = true;
				}
				viewStack.loadStartup();
				
				if(Config.instance.debug){
					var flexSpyButton:Button = new Button();
					flexSpyButton.label="FlexSpy";
					flexSpyButton.addEventListener(MouseEvent.CLICK,showFlexSpy);
					topBar.addChild(flexSpyButton);
				}
			}
			
			private function showFlexSpy(event:Event):void{
				FlexSpy.show();
			}

		]]>
	</mx:Script>

	<mx:Panel id="appPanel" width="100%"
			  height="100%"
			  layout="absolute"
			  title="Clarin Component Browser"
			  x="0"
			  y="0">			  

		<common:RegistryViewStack id="viewStack"
								  borderStyle="none"
								  height="100%"
								  width="100%"/>
		
		<mx:HBox borderStyle="none" id="topBar" right="0">
			<mx:Label text="User: {Credentials.instance.userName}"/>
			<common:LoginLabelButton id="login" includeInLayout="{!Credentials.instance.isLoggedIn()}" visible="{!Credentials.instance.isLoggedIn()}" />
			<common:UserSettingsLabelButton id="userSettings" includeInLayout="{Credentials.instance.isLoggedIn()}" visible="{Credentials.instance.isLoggedIn()}"/>
		</mx:HBox>
	</mx:Panel>
		
</mx:Application>
